behavior logolang;

use java.lang.Math;
use fr.inria.diverse.logo.tools.Util;

class SymbolTable {
	logolang::SymbolTable parent;
	Sequence(logo::Symbol) symbols;
	
	def void init(logolang::SymbolTable parent) {
		self.parent := parent;
		self.symbols := Sequence{};
	}

    def void init() {
    	self.init(null);
    }
	
	def void register(logo::Symbol symbol) {
		self.symbols += symbol;
	}
	
	def logo::Symbol find(String name) {
		logo::Symbol found := self.symbols->any(s | s.name = name);
		if (found = null and self.parent != null) {
			found := self.parent.find(name);
		}
		result := found;
	}
}

class Segment {
	double x1;
	double x2;
	double y1;
	double y2;
	
	def String representation() {
		result := '(' + self.x1 + ', ' + self.x2 + ', ' + self.y1 + ', ' + self.y2 + ')';
	}
}

class Canvas {
	Sequence(logolang::Segment) segments;
}

class Turtle {
	double xpos;
	double ypos;
	double direction;
	boolean pendown;
	logolang::Canvas canvas;
}

open class Logo {
    @step
	def void run(logolang::Turtle turtle, logolang::SymbolTable st) {
		for (s in self.statements) {
			s.execute(turtle, st);
		}
	}
}

open class Interpreter {		
	logolang::Turtle turtle;
	logolang::SymbolTable st;
	
	@init
	def void init() {
		self.turtle := logolang::Turtle.create();
		self.turtle.xpos := 0.0;
		self.turtle.ypos := 0.0;
		self.turtle.direction := 0.0;
		self.turtle.pendown := false;
		self.turtle.canvas := logolang::Canvas.create();
		self.turtle.canvas.segments := Sequence{};
		self.st := logolang::SymbolTable.create();
		self.st.init();
	}

	@main
	def void run() {
		if (self.instruction.oclIsKindOf(logo::Statement)) {
			logo::Statement statement := self.instruction;
			statement.execute(self.turtle, self.st);
			('x: ' + self.turtle.xpos + ' | y: ' + self.turtle.ypos + ' | direction: ' + self.turtle.direction
				+ ' | canvas: ' + self.turtle.canvas.segments->collect(seg | seg.representation())).log();
		} else {
			if (self.instruction.oclIsKindOf(logo::Expression)) {
				logo::Expression expression := self.instruction;
				logo::Value res := expression.compute(self.st);
				if (res = null) {
					'null'.log();
				} else {
					res.value.log();
				}
			} else {
				if (self.instruction.oclIsKindOf(logo::Logo)) {
					logo::Logo logo := self.instruction;
					logo.run(self.turtle, self.st);
				}
			}
		}
	}
}

open class Variable {
	Value value := null;
	
	def void init(Parameter definition) {
		self.definition := definition;
		self.name := definition.name;
	}
	
	def Value getValue() {
		result := self.value;
	}

    def void setValue(Value value) {    	
    	self.value := value;
    }
}

open class Procedure {
	ProcedureDefinition definition := null;
	
	def void init(ProcedureDefinition definition) {
		self.definition := definition;
		self.name := definition.name;
	}
}

open class Left {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logo::Value value := self.angle.compute(st);
		if (value.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue := value;
		    turtle.direction := turtle.direction - nvalue.value.toDouble();
		    while (turtle.direction < -180.0) {
		    	turtle.direction := turtle.direction + 360.0;
		    }
		}
	}
}
open class Right {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logo::Value value := self.angle.compute(st);
		if (value.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue := value;
		    turtle.direction := turtle.direction + nvalue.value.toDouble();
		    while (turtle.direction > 180.0) {
		    	turtle.direction := turtle.direction - 360.0;
		    }
		}
	}
}
open class Forward {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logo::Value value := self.step.compute(st);
		if (value.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue := value;
			logo::Segment segment := null;
			if (turtle.pendown) {
				segment := logolang::Segment.create();
				segment.x1 := turtle.xpos;
				segment.y1 := turtle.ypos;
			}
			turtle.xpos := turtle.xpos + nvalue.value.toDouble() * turtle.direction.toRadians().cos();
			turtle.ypos := turtle.ypos + nvalue.value.toDouble() * turtle.direction.toRadians().sin();
			if (turtle.pendown) {
				segment.x2 := turtle.xpos;
				segment.y2 := turtle.ypos;
				turtle.canvas.segments += segment;
			}
		}
	}
}
open class PenDown {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		turtle.pendown := true;
	}
}
open class PenUp {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		turtle.pendown := false;
	}
}
open class ProcedureDefinition {
	def void execute(Turtle turtle, SymbolTable st) {
		symbol::Procedure procedure := symbol::Procedure.create();
		procedure.init(self);
		st.register(procedure);
	}
}
open class ProcedureCall {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		ProcedureDefinition definition := st.find(self.name).definition;
		logolang::SymbolTable nst := logolang::SymbolTable.create();
		nst.init(st);
		int i := 1;
		while (i <= definition.parameters->size()) {
			symbol::Variable variable := symbol::Variable.create();
			variable.init(definition.parameters->at(i));
			variable.setValue(self.parameterValues->at(i).compute(st));
			nst.register(variable);
			i := i + 1;
		}
		for (s in definition.statements) {
			s.execute(turtle, nst);
		}
	}
}
open class Block {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logolang::SymbolTable nst := logolang::SymbolTable.create();
		nst.init(st);
		for (s in self.statements) {
			s.execute(turtle, nst);
		}
	}
}
open class If {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logo::Value value := self.condition.compute(st);
		if (value.oclIsKindOf(value::BoolValue)) {
			value::BoolValue nvalue := value;
			if (nvalue.value) {
				self.ifBlock.execute(turtle, st);
			} else {
				if (self.elseBlock != null) {
					self.elseBlock.execute(turtle, st);
				}
			}
		}
	}
}
open class While {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		while (self.condition.compute(st)) {
			self.block.execute(turtle, st);
		}
	}
}
open class Repeat {
	@step
	def void execute(Turtle turtle, SymbolTable st) {
		logo::Value value := self.condition.compute(st);
		if (value.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue := value;
			int nbRepetitions := nvalue.value;
			while (nbRepetitions > 0) {
				self.block.execute(turtle, st);
				nbRepetitions := nbRepetitions - 1;
			}
		}
	}
}

open class logo.expression.constant.IntValue {
	@step
	def Value compute(SymbolTable st) {
		value::IntValue rvalue := value::IntValue.create();
		rvalue.value := self.value;
		result := rvalue;
	}
}
open class logo.expression.constant.BoolValue {
	@step
	def Value compute(SymbolTable st) {
		value::BoolValue rvalue := value::BoolValue.create();
		rvalue.value := self.value;
		result := rvalue;
	}
}
open class Not {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value := self.expression.compute(st);
		if (value.oclIsKindOf(value::BoolValue)) {
			value::BoolValue nvalue := value;
			value::BoolValue rvalue := value::BoolValue.create();
			rvalue.value := not nvalue.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Opposite {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value := self.expression.compute(st);
		if (value.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue := value;
			value::IntValue rvalue := value::IntValue.create();
			rvalue.value := -nvalue.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Minus {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::IntValue.create();
			rvalue.value := nvalue1.value - nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Plus {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::IntValue.create();
			rvalue.value := nvalue1.value + nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Mult {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::IntValue.create();
			rvalue.value := nvalue1.value * nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Div {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::IntValue.create();
			rvalue.value := nvalue1.value / nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Equals {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::BoolValue.create();
			rvalue.value := nvalue1.value = nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Greater {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::BoolValue.create();
			rvalue.value := nvalue1.value > nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class Lower {
	@step
	def Value compute(SymbolTable st) {
		logo::Value value1 := self.leftExpression.compute(st);
		logo::Value value2 := self.rightExpression.compute(st);
		if (value1.oclIsKindOf(value::IntValue) and value2.oclIsKindOf(value::IntValue)) {
			value::IntValue nvalue1 := value1;
			value::IntValue nvalue2 := value2;
			value::IntValue rvalue := value::BoolValue.create();
			rvalue.value := nvalue1.value < nvalue2.value;
			result := rvalue;
		} else {
			result := null;
		}
	}
}
open class logo.expression.extended.And {
	@step
	def Value compute(SymbolTable st) {
		value::BoolValue rvalue := value::BoolValue.create();
		rvalue.value := true;
		for (e in self.expressions) {
		    logo::Value value := e.compute(st);
		    if (value.oclIsKindOf(value::BoolValue) and rvalue != null) {
				value::BoolValue nvalue := value;
				rvalue.value := rvalue.value and nvalue.value;
			} else {
				rvalue := null;
			}
		}
		result := rvalue;
	}
}
open class logo.expression.extended.Or {
	@step
	def Value compute(SymbolTable st) {
		value::BoolValue rvalue := value::BoolValue.create();
		rvalue.value := false;
		for (e in self.expressions) {
		    logo::Value value := e.compute(st);
		    if (value.oclIsKindOf(value::BoolValue) and rvalue != null) {
				value::BoolValue nvalue := value;
				rvalue.value := rvalue.value or nvalue.value;
			} else {
				rvalue := null;
			}
		}
		result := rvalue;
	}
}
open class VariableRead {
	@step
	def Value compute(SymbolTable st) {
		Variable found := st.find(self.name);
		if (found != null) {
			result := found.getValue();
		} else {
			result := null;
		}
	}
}